buildscript {
    def kotlin_version = project.properties['RNSAC_kotlinVersion']
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

plugins {
    id("com.android.library")
    id("kotlin-android")
}

def kotlin_version = project.properties['RNSAC_kotlinVersion']

def reactNativeRootDir = new File("$projectDir/../node_modules/react-native")

android {
    compileSdk = 31

    ndkVersion "21.4.7075529"

    defaultConfig {
        minSdk = 21
        targetSdk = 31
        buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", "true"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles("consumer-rules.pro")
        externalNativeBuild {
            cmake {
                arguments "-DREACT_COMMON_DIR=${reactNativeRootDir}/ReactCommon",
                "-DREACT_ANDROID_DIR=${reactNativeRootDir}/ReactAndroid",
                "-DREACT_ANDROID_BUILD_DIR=${reactNativeRootDir}/ReactAndroid/build",
                "-DANDROID_STL=c++_shared",
                "-DANDROID_TOOLCHAIN=clang",
                "-DANDROID_PLATFORM=android-21"
                cppFlags "-std=c++17"
            }
        }

        ndk {
            abiFilters "armeabi-v7a", "x86", "x86_64", "arm64-v8a"
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        // For some reason gradle only complains about the duplicated version of libreact_render libraries
        // while there are more libraries copied in intermediates folder of the lib build directory, we exlude
        // only the ones that make the build fail (ideally we should only include libsafeareacontext_modules but we
        // are only allowed to specify exlude patterns)
        exclude "**/libf*.so"
        exclude "**/libfolly_*.so"
        exclude "**/libglog.so"
        exclude "**/libjsi.so"
        exclude "**/libreact_*.so"
        exclude "**/librrc_*.so"
        exclude "**/libturbo*.so"
        exclude "**/liby*.so"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
        }
    }
    externalNativeBuild {
        cmake {
            path("src/main/jni/CMakeLists.txt")
            version = "3.18.1"
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation("androidx.appcompat:appcompat:1.5.1")
    implementation("com.google.android.material:material:1.4.0")
    testImplementation("junit:junit:4.13.2")
    androidTestImplementation("androidx.test.ext:junit:1.1.3")
    androidTestImplementation("androidx.test.espresso:espresso-core:3.4.0")
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation project(":ReactAndroid")
}
